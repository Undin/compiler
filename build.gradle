buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.6'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'antlr'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.7
version = 1.0

repositories {
    jcenter()
}

shadowJar {
    classifier = null
    manifest {
        attributes 'Main-Class' : 'com.warrior.compiler.MainKt'
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            println("\n$output\n")
        }
    }
}

generateGrammarSource {
    arguments += ["-visitor", "-no-listener"]
}
compileKotlin.dependsOn generateGrammarSource

dependencies {
    antlr 'org.antlr:antlr4:4.6'

    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.6'
    compile 'org.bytedeco.javacpp-presets:llvm-platform:3.9.0-1.3'

    testCompile 'junit:junit:4.12'
}
